plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'me.yuugiri.fluiditygradle'
}

group = 'me.yuugiri.mod'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.20'
    include('com.github.mccheatz:HookUtility:01537cc1d9') {
        exclude group: 'org.ow2.asm'
    }
    implementation 'org.ow2.asm:asm-all:5.0.3' // forge 1.8.9 asm version
    implementation MinecraftDependencyKt.minecraftDep(project, "src/main/resources/fluidity_at.cfg")
    implementation MinecraftDependencyKt.launchWrapper(project)
    implementation MinecraftDependencyKt.minecraftforge(project)
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/*.kotlin_module"
    exclude "META-INF/**/*.pro"
    exclude "LICENSE.txt"
    exclude "**/module-info.class"
    exclude "**/package-info.class"
    exclude "**/*.kotlin_metadata"
    exclude "**/*.kotlin_builtins"

    manifest {
        attributes["FMLCorePlugin"] = "me.yuugiri.mod.inject.FMLLoadingPlugin"
        attributes["FMLCorePluginContainsFMLMod"] = "true"
        attributes["ModSide"] = "CLIENT"
    }
}

genIntelliJRuns {
    vmParams = "-Dfml.coreMods.load=me.yuugiri.mod.inject.FMLLoadingPlugin"
}
classes.dependsOn(putObfMap)
reobfJarWithStrip.dependsOn(shadowJar)
tasks.getByName("build").dependsOn(reobfJarWithStrip)